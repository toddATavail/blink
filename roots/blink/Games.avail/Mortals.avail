/*
 * Mortals.avail
 * Copyright (c) Todd L Smith and Taylor DH Smith, 2017.
 * All rights reserved.
 */

Module "Mortals"
Uses
	"Blink"
Body

/* These are the public states, available by IR. */
"active", "inactive", and "dead" are public states.

/* The range type specifies modular arithmetic. */
"team" is [1..2].

"initialize" is a signal.
"set team color" is a signal.
On boot, now initialize.
To initialize:
[
	/* Note that "_:=_" is equivalent to "Change_to_", and "my_" is always
	 * optional for global variables. For less verbosity, we could rewrite:
	 *
	 * Change my team to 1.
	 *
	 * As either:
	 *
	 * Change team to 1.
	 *
	 * Or:
	 *
	 * team := 1.
	 */
	Change my team to 1.
	Change my lighting mode to constant.
	Change my state to dead.
	Now set team color.
].

"change sides" is a signal.
On long click, now change sides.
On double click, now change sides.
To change sides:
[
	/* This is modular arithmetic; wrap around is automatic. */
	team += 1.
	Now set team color.
].

To set team color:
[
	Change my color to
		if my state is inactive,
			if team = 1, green;
			else, magenta;
		else,
			if team = 1, dark green;
			else, dark magenta.
].

"lifetime" is a timer.
"begin play" is a signal.
On triple click, now begin play.
When any dead neighbor becomes inactive, now begin play.
To begin play:
[
	Change my state to inactive.
	Expire my lifetime in 60s.
	
	Change my lighting mode to pulsing.
	Change my pattern to sinusoidal.
	Change my pattern duration to 2s.
	Now set team color.
].

When I become isolated, if my state is inactive:
[
	Change my state to active.
	Change my color to white.
].

/* Since I cannot command any other Blinks to do anything — I can only
 * advertise my own state and consider my neighbors' advertised states — I must
 * decide independently what to do based on what I know.
 */
When I join any neighbors:
[
	If my state is inactive and any of these neighbors are active:
	[
		Adjust my lifetime by -5s.
		/* Pulse my light black for 1s. */
	].
	If my state is active:
	[
		Adjust my lifetime by 5s × count of these neighbors that are inactive.
		Change my state to inactive.
		Now set team color.
		/* If inactiveCount, pulse my light white for 1s. */
	].
].

When my lifetime has 30s remaining, change my pattern duration to 1000ms.
When my lifetime has 10s remaining, change my pattern duration to 500ms.

When my lifetime expires:
[
	Change my state to dead.
	Change my lighting mode to constant.
	Now set team color.
].
