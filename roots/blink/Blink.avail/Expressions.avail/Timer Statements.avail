/*
 * Timer Statements.avail
 * Copyright (c) Todd L Smith and Taylor DH Smith, 2017.
 * All rights reserved.
 */

Module "Timer Statements"
Uses
	"Abstract Syntax Tree",
	"Avail"
Names
	"(Blink)_is a timer."
Body

/******************************************************************************
 *                       Timer declaration statements.                       *
 ******************************************************************************/

/**
 * Declare a {@type "timer"}.
 *
 * @macro "(Blink)_is a timer."
 * @param "tNamePhrase" "expression phrase ⇒ nonempty string literal"
 * @returns "sequence phrase"
 */
Public macro "(Blink)_is a timer." is
[
	tNamePhrase : expression phrase ⇒ nonempty string literal
|
	tName ::= evaluate tNamePhrase;
	t ::= a timer with
		timer name ::= tName's value,
		expression type ::= ⊤;
	tStmts ::=
	<
		send $"Method_is_" with
		«<
			‘t's name’,
			«[<‘t’>] : t's type»
		>» : ⊤ → statement phrase
	>;
	tDeclExpr ::= a timer declaration statement with
		expression type ::= ⊤,
		declared timer ::= t;
	recordStmts ::= statement recorder for tDeclExpr;
	stmts ::= tStmts ++ recordStmts;
	seq «stmts»
];