/*
 * Lighting Modes.avail
 * Copyright (c) Todd L Smith and Taylor DH Smith, 2017.
 * All rights reserved.
 */

Module "Lighting Modes"
Uses
	"Abstract Syntax Tree",
	"Avail"
Names
	/* Lighting modes. */
	"lighting mode",
		"constant",
		"pulsing"
Body

/******************************************************************************
 *                              Lighting modes.                               *
 ******************************************************************************/

/**
 * Answer a {@type "literal"} that denotes constant lighting.
 *
 * @macro "constant"
 * @return "atom literal"
 */
[
	a ::= new atom named "constant";
	lit ::= a literal with expression type ::= (a's type);
	Public macro "constant" is [‘lit’];
]();

/**
 * Answer a {@type "literal"} that denotes pulsing lighting.
 *
 * @macro "pulsing"
 * @return "atom literal"
 */
[
	a ::= new atom named "pulsing";
	lit ::= a literal with expression type ::= (a's type);
	Public macro "pulsing" is [‘lit’];
]();

/**
 * A {@type "lighting mode"} denotes the possible lighting modes of a Blink.
 * The modes correspond to built-in functions that govern the behavior of the
 * RGB LED.
 *
 * @type "lighting mode"
 * @supertype "atom"
 */
Public macro "lighting mode" is [‘{constant's value, pulsing's value}ᵀ’];
